plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'net.minecrell.licenser' version '0.2.1'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'ninja.miserable.blossom' version '1.0.1'
}
defaultTasks 'clean', 'licenseFormat'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'uk.jamierocks.mana'
archivesBaseName = project.name.toLowerCase()
description = 'The framework powering the Mana IRC Bot.'
version '1.1.1'
ext.url = 'http://mana.jamierocks.uk/'

repositories {
    mavenCentral()
    maven {
        url 'http://repo.miserable.ninja/content/repositories/public/'
    }
}

dependencies {
    // Basic dependencies
    compile 'com.google.guava:guava:19.0'
    compile 'com.google.inject:guice:4.0'

    // Configuration related
    compile 'ninja.leaping.configurate:configurate-core:3.1.1'
    compile 'ninja.leaping.configurate:configurate-hocon:3.1.1'

    // Logging related
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.apache.logging.log4j:log4j-api:2.6.1'
    compile 'org.apache.logging.log4j:log4j-core:2.6.1'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.6.1'

    // IRC related
    compile 'org.kitteh.irc:client-lib:1.1.0'

    // Command related
    compile 'com.sk89q.intake:intake:4.2-MISNIN-SNAPSHOT'
}

license {
    header file('HEADER.txt')
    include '**/*.java'
    include '**/*.groovy'
}

blossom {
    replaceToken '%version%', project.version, 'src/main/java/uk/jamierocks/mana/carbon/util/Constants.java'
}

jar {
    classifier = 'base'

    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Main-Class': 'uk.jamierocks.mana.carbon.Main'
        )
    }
}

shadowJar {
    classifier = ''
}

task javadocJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar, dependsOn: 'classes') {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives shadowJar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (System.getenv('MAVEN_RELEASES') != null) {
                repository(url: 'file://' + System.getenv('MAVEN_RELEASES'))
                snapshotRepository(url: 'file://' + System.getenv('MAVEN_SNAPSHOTS'))
            }

            pom {
                artifactId = project.archivesBaseName

                project {
                    name = project.name
                    description = project.description
                    packaging = 'jar'
                    url = project.url

                    scm {
                        url = "https://github.com/ManaBot/Carbon"
                        connection = "scm:git:https://github.com/ManaBot/Carbon.git"
                        developerConnection = "scm:git:git@github.com:ManaBot/Carbon.git"
                    }

                    issueManagement {
                        system = 'GitHub'
                        url = 'https://github.com/ManaBot/Carbon/issues'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
