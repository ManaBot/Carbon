buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.net.minecrell:licenser:0.2.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath 'gradle.plugin.ninja.miserable:blossom:1.0.1'
    }
}
defaultTasks 'clean', 'licenseFormat'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    apply plugin: 'net.minecrell.licenser'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group 'uk.jamierocks.mana'
    archivesBaseName = project.name.toLowerCase()
    description = 'The framework powering the Mana IRC Bot.'
    version '2.0.0-SNAPSHOT'
    ext.url = 'http://mana.jamierocks.uk/'

    repositories {
        mavenCentral()
        maven {
            name 'lexteam-release'
            url 'http://repo.lexteam.xyz/maven/releases/'
        }
        maven {
            name 'lexteam-snapshot'
            url 'http://repo.lexteam.xyz/maven/snapshots/'
        }
        maven {
            name 'sk89q-public'
            url 'http://maven.sk89q.com/repo/'
        }
    }

    license {
        header rootProject.file('HEADER.txt')
        include '**/*.java'
    }

    shadowJar {
        classifier = 'shaded'
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar, dependsOn: 'classes') {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives shadowJar
        archives javadocJar
        archives sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                if (System.getenv('MAVEN_RELEASES') != null) {
                    repository(url: 'file://' + System.getenv('MAVEN_RELEASES'))
                    snapshotRepository(url: 'file://' + System.getenv('MAVEN_SNAPSHOTS'))
                }

                pom {
                    artifactId = project.archivesBaseName

                    project {
                        name = project.name
                        description = project.description
                        packaging = 'jar'
                        url = project.url

                        scm {
                            url = "https://github.com/ManaBot/Carbon"
                            connection = "scm:git:https://github.com/ManaBot/Carbon.git"
                            developerConnection = "scm:git:git@github.com:ManaBot/Carbon.git"
                        }

                        issueManagement {
                            system = 'GitHub'
                            url = 'https://github.com/ManaBot/Carbon/issues'
                        }

                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'http://opensource.org/licenses/MIT'
                                distribution = 'repo'
                            }
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}
